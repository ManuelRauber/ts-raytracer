#version 460
#extension GL_GOOGLE_include_directive : enable

#include "utils.comp"
#include "materials.comp"
#include "ray.comp"

//#define FLT_MAX 3.402823466e+38
#define FLT_MAX 99999.99
//const float INFINITY     = 1. / 0.;
const uint SPHERE_COUNT = 4;

layout(set = 0, binding = 0) uniform ComputeParams {
  float fWidth;
  float fHeight;
  float fSamplesPerPixel;
  float fMaxBounces;
}
params;

layout(std140, set = 0, binding = 1) buffer PixelBuffer {
  vec4 pixels[];
}
pixelBuffer;

struct Sphere {
  vec3  center;
  float radius;
  int   materialType;
  vec3  albedo;
  float roughness;
};

bool hitSphere(const Sphere s, const Ray r, float t_min, float t_max, inout HitRecord rec) {
  vec3  oc           = r.origin - s.center;
  float a            = lengthSquared(r.direction);
  float half_b       = dot(oc, r.direction);
  float c            = lengthSquared(oc) - s.radius * s.radius;
  float discriminant = half_b * half_b - a * c;

  if (discriminant > 0) {
    float root = sqrt(discriminant);
    float temp = (-half_b - root) / a;
    if (temp < t_max && temp > t_min) {
      rec.t               = temp;
      rec.p               = rayAt(r, rec.t);
      vec3 outward_normal = (rec.p - s.center) / s.radius;
      setFaceNormal(rec, r, outward_normal);
      rec.materialType = s.materialType;
      rec.albedo       = s.albedo;
      rec.roughness    = s.roughness;
      return true;
    }
    temp = (-half_b + root) / a;
    if (temp < t_max && temp > t_min) {
      rec.t               = temp;
      rec.p               = rayAt(r, rec.t);
      vec3 outward_normal = (rec.p - s.center) / s.radius;
      setFaceNormal(rec, r, outward_normal);
      rec.materialType = s.materialType;
      rec.albedo       = s.albedo;
      rec.roughness    = s.roughness;
      return true;
    }
  }
  return false;
}

bool hittablesHit(const Sphere spheres[SPHERE_COUNT], const Ray r, float t_min, float t_max, inout HitRecord rec) {
  HitRecord temp_rec;
  bool      hit_anything   = false;
  float     closest_so_far = t_max;

  for (int i = 0; i < SPHERE_COUNT; i++) {
    if (hitSphere(spheres[i], r, t_min, closest_so_far, temp_rec)) {
      hit_anything   = true;
      closest_so_far = temp_rec.t;
      rec            = temp_rec;
    }
  }

  return hit_anything;
}

vec3 rayColor(Ray ray, const Sphere spheres[SPHERE_COUNT], uint depth) {
  HitRecord rec;
  vec3      color = vec3(1.0, 1.0, 1.0);

  for (int i = 0; i < depth; i++) {
    if (hittablesHit(spheres, ray, 0.001, FLT_MAX, rec)) {
      Ray  newRay;
      vec3 attenuation;

      bool wasScattered = materialBSDF(ray, rec, attenuation, newRay);

      ray.origin    = newRay.origin;
      ray.direction = newRay.direction;

      if (wasScattered) {
        color *= attenuation;
      } else {
        color *= vec3(0.0, 0.0, 0.0);
        break;
      }

    } else {
      vec3  unit_direction = normalize(ray.direction);
      float t              = 0.5 * (unit_direction.y + 1.0);
      vec3  skyColor       = (1.0 - t) * vec3(1.0, 1.0, 1.0) + t * vec3(0.5, 0.7, 1.0);
      color *= skyColor;
      break;
    }
  }

  return color;
}

void main() {
  uint index = gl_GlobalInvocationID.x;

  float j = params.fHeight - (floor(float(index) / params.fWidth));
  float i = mod(float(index), params.fWidth);

  initSeed(index * 1099087573U);
  //initSeed(vec2(i, j));

  float aspect_ratio      = params.fWidth / params.fHeight;
  float viewport_height   = 2.0;
  float viewport_width    = aspect_ratio * viewport_height;
  float focal_length      = 1.0;
  vec3  origin            = vec3(0.0, 0.0, 0.0);
  vec3  horizontal        = vec3(viewport_width, 0, 0);
  vec3  vertical          = vec3(0, viewport_height, 0);
  vec3  lower_left_corner = origin - horizontal / 2 - vertical / 2 - vec3(0, 0, focal_length);

  Sphere spheres[SPHERE_COUNT] = {

      {{0.0, 0.0, -1.0}, 0.5, MATERIAL_LAMBERT, {0.7, 0.3, 0.3}, 0.0},

      {{0.0, -100.5, -1.0}, 100, MATERIAL_LAMBERT, {0.8, 0.8, 0.8}, 0.0},

      {{-1.0, 0.0, -1.0}, 0.5, MATERIAL_METAL, {0.8, 0.8, 0.8}, 0.1},
      {{1.0, 0.0, -1.0}, 0.5, MATERIAL_METAL, {0.8, 0.6, 0.2}, 0.8},

  };

  vec3 pixel_color = vec3(0.0, 0.0, 0.0);

  const uint ssp     = uint(params.fSamplesPerPixel);
  const uint bounces = uint(params.fMaxBounces);

  for (int s = 0; s < ssp; s++) {
    float u = (i + random()) / (params.fWidth - 1.0);
    float v = (j + random()) / (params.fHeight - 1.0);
    Ray   r = Ray(origin, lower_left_corner + u * horizontal + v * vertical - origin);
    pixel_color += rayColor(r, spheres, bounces);
  }

  float scale = 1.0 / params.fSamplesPerPixel;
  pixel_color *= scale;
  //pixel_color = vec3(random());

  // gamma correction
  pixel_color = sqrt(pixel_color);

  //pixel_color               = randomVec3();
  pixelBuffer.pixels[index] = vec4(pixel_color, 1.0);
}

/*

  0 1 2 3
0 * * * *
1 * * * *
2 * * * *
3 * * * *

11

*/
